DEFINE_ACTION_FUNCTION 20190204_1 BEGIN

	OUTER_SPRINT fix_root ~%mod_root%/_fix/20190204~
	
	//任意门动画错误复制=
	ACTION_IF FILE_EXISTS ~override/spdimndr.bam~ BEGIN DELETE ~override/spdimndr.bam~ END
	ACTION_IF FILE_EXISTS ~override/spdimndr.vvc~ BEGIN DELETE ~override/spdimndr.vvc~ END
	
	//卡顿	
	INCLUDE ~%fix_root%/observers.tph~
		
	ACTION_PHP_EACH old_observers AS creature => conditions BEGIN		
		OUTER_FOR (slot=1;slot<7;++slot) BEGIN
			COPY_EXISTING ~%creature_0%%slot%.bcs~ ~override/%creature_1%%slot%.bcs~
		END
		
		ACTION_IF ~%conditions%~ STR_EQ ~NONE~ BEGIN OUTER_SPRINT conditions ~~ END

		<<<<<<<< .../c4_inlined/create_observer_baldur.baf
			IF
				InPartyAllowDead(Player%slot%)
				%conditions%		
				!GlobalTimerNotExpired("%name%%c4obvNUM%_Exists_Timer","Global")
			THEN
				RESPONSE #100					
					CreateCreatureImpassable("%name%%c4obvNUM%",[1.1],0)
					Continue()
			END
		>>>>>>>>
		
		LAF remove_observer_old INT_VAR remove_script=1 STR_VAR name=~%creature_0%~ conditions END

		<<<<<<<< .../c4_inlined/create_observer_baldur.baf
		IF
			InPartyAllowDead(Player%slot%)
			%conditions%
			!TriggerOverride(Player%slot%,InMyArea("%name%%c4obvNUM%"))
			!GlobalTimerNotExpired("%name%%c4obvNUM%_Exists_Timer","Global")
		THEN
			RESPONSE #100
				SetGlobalTimer("%name%%c4obvNUM%_Exists_Timer","Global",6)
				CreateCreatureImpassable("%name%%c4obvNUM%",[1.1],0)
				Continue()
		END
		>>>>>>>>

		LAF create_observer_old INT_VAR create_script=0 STR_VAR name=~%creature_1%~ conditions END
	END
	
	//mrk
	COPY_EXISTING ring01.itm ~%marker_loc%/20190204_1.mrk~

END
