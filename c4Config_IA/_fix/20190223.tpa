DEFINE_ACTION_FUNCTION 20190223 BEGIN

	OUTER_SPRINT fix_root ~%mod_root%/_fix/20190223~
		
	//挡路
	ACTION_DEFINE_ASSOCIATIVE_ARRAY old_observers BEGIN
		c4efOBV ,	c4efO	,		c4efO_		=>		~NONE~
		c4MonkP ,	c4MonkO ,	c4mkO_		=>		~Kit(Player%slot%,c4Monk)~
		c4cbOBV ,	c4cbO	,		c4cbO_		=>		~NONE~
		c4IK1 ,		c4IK1O	,	c4IK1O_		=>		~Kit(Player%slot%,WIZARDSLAYER)~
		c4IK2 ,		c4IK2O	,	c4IK2O_		=>		~Kit(Player%slot%,BEASTMASTER)~
		c4QA ,		c4QAO	,		c4QAO_		=>		~Kit(Player%slot%,c4Q06Fighter)~
		c4QB ,		c4QBO	,		c4QBO_		=>		~Kit(Player%slot%,c4Q06Paladin)~
		c4QC ,		c4QCO	,		c4QCO_		=>		~Kit(Player%slot%,c4Q06Thief)~
		c4QD ,		c4QDO	,		c4QDO_		=>		~Kit(Player%slot%,c4Q06Shaman)~
	END

	ACTION_PHP_EACH old_observers AS creature => conditions BEGIN		
		OUTER_FOR (slot=1;slot<7;++slot) BEGIN
			COPY_EXISTING ~%creature_2%%slot%.cre~ override
				WRITE_ASCII 0x258 ~c4efo1~ (8)
		END
	END
	
	COPY_ALL_GAM_FILES
		PATCH_FOR_EACH member IN 0x20 0x30 BEGIN
			READ_LONG member player_offset
			READ_LONG member+0x4 player_count
			FOR (i=0;i<player_count;++i) BEGIN
				npc_offset=player_offset+i*0x160
				READ_LONG npc_offset+0x4 cre_offset
				READ_ASCII cre_offset+0x280 deathvar
				PHP_EACH old_observers AS creature => conditions BEGIN
					PATCH_IF NOT ~%deathvar%~ STRING_CONTAINS_REGEXP ~%creature_2%~ BEGIN
						WRITE_LONG cre_offset+0x8 0
						WRITE_LONG cre_offset+0xc 0
						WRITE_LONG cre_offset+0x28 0xd100
						WRITE_ASCII cre_offset+0x258 ~c4efo1~ (8)
					END
				END
			END
		END
		BUT_ONLY
	
	//跟随影响敌人？
	COPY_EXISTING_REGEXP GLOB ~.*\.bcs~ override
		DECOMPILE_AND_PATCH BEGIN
			LPF REPLACE_MULTILINE INT_VAR warn=0 verbose=0 STR_VAR
				pattern = ~\(Global("c4_Summoned_Follow","Global",1)\)~
				string = ~OR(2) \1 Allegiance(Myself,ENEMY)~
			END
		END
		BUT_ONLY

	/* 避免海尔达利剧情死亡 */
	EXTEND_TOP ~HAERDALI.bcs~ ~%fix_root%/haeridalis_minhp1_joined.baf~
		
	//法多
	COPY_EXISTING cefaldor.dlg override
		DECOMPILE_AND_PATCH BEGIN
			REPLACE_TEXTUALLY ~!True()%LNL%\(!?IfValidForPartyDialogue("Cernd")\)~ ~Global("c4DruidChallangeFaldor","Global",0) \1~
			REPLACE_TEXTUALLY ~!True()%LNL%\(!?IfValidForPartyDialogue("Jaheira")\)~ ~Global("c4DruidChallangeFaldor","Global",0) \1~
		END

	//绝地7型持续时间
	COPY_EXISTING_REGEXP GLOB ~baldur.*\.bcs~ override
		DECOMPILE_AND_PATCH BEGIN
			LPF REPLACE_MULTILINE INT_VAR verbose=0 STR_VAR
				pattern = 
				~IF
					Class(Player[1-6],PALADIN)
					OR(4)
						Kit(Player[1-6],c4JediA)
						Kit(Player[1-6],c4JediB)
						Kit(Player[1-6],c4JediC)
						Kit(Player[1-6],c4JediD)
					TriggerOverride(Player[1-6],Global("c4EF_Jedi_Style_Form7","Locals",0))
					!LevelLT(Player[1-6],.+)
					LevelLT(Player[1-6],.+)
				THEN
					RESPONSE #100
						ReallyForceSpellRES("c4efJj7Y",Player[1-6])
						SetGlobalTimer("c4EF_Form7_Player[1-6]","Global",.+)
						Continue()
				END~
			END
			
			LPF REPLACE_MULTILINE INT_VAR verbose=0 STR_VAR 
				pattern = 
				~IF
					Class(Player\([1-6]\),PALADIN)
					OR(4)
						Kit(Player[1-6],c4JediA)
						Kit(Player[1-6],c4JediB)
						Kit(Player[1-6],c4JediC)
						Kit(Player[1-6],c4JediD)
					TriggerOverride(Player[1-6],Global("c4EF_Jedi_Style","Locals",\([1-6]\)))
					!HasItemEquiped("c4efJj[1-6]",Player[1-6])
					TriggerOverride(Player[1-6],Global("c4EF_Jedi_Style_Form7","Locals",1))
					!GlobalTimerNotExpired("c4EF_Form7_Player[1-6]","Global")
				THEN
					RESPONSE #100
						ReallyForceSpellRES("c4efJj7[1-6]",Player[1-6])
						Continue()
				END~
				string =
				~IF
					Class(Player\1,PALADIN)
					OR(4)
						Kit(Player\1,c4JediA)
						Kit(Player\1,c4JediB)
						Kit(Player\1,c4JediC)
						Kit(Player\1,c4JediD)
					TriggerOverride(Player\1,Global("c4EF_Jedi_Style","Locals",\2))
					!HasItemEquiped("c4efJj\2",Player\1)
					TriggerOverride(Player\1,Global("c4EF_Jedi_Style_Form7","Locals",1))
					CheckStat(Player\1,0,187)
				THEN
					RESPONSE #100
						ActionOverride(Player\1,ReallyForceSpellRES("c4efjj7\2",Player\1))
						Continue()
				END~
			END
		END
		BUT_ONLY
	
	COPY_EXISTING c4efjj7y.spl override
		LPF CLONE_EFFECT INT_VAR opcode=297 timing=0 STR_VAR resource=~~ END
		LPF CREATE_HEADERS_AS_LEVEL INT_VAR max_level=34 level_increment=20 level_multiplier=2 END
		LPF ALTER_HEADER_EFFECTS_AS_LEVEL INT_VAR match_opcode=297 duration=12 multi_duration=6 END
		
	COPY_EXISTING c4efjj7.itm override
		LPF ADD_ITEM_EQEFFECT INT_VAR opcode=146 target=1 timing=1 STR_VAR resource=~c4efjj7y~ END

	//混元说明
	OUTER_SPRINT c4monk_old @190223004
	OUTER_SPRINT c4monk_new @190213002
	
	//5型
	COPY_EXISTING c4efjj5t.spl override
		LPF CREATE_HEADERS_AS_LEVEL INT_VAR max_level=30 level_increment=21 level_multiplier=9 END
		LPF ALTER_HEADER_EFFECTS_AS_LEVEL INT_VAR match_opcode=250 duration=2 multi_duration=2 END
		LPF ALTER_HEADER_EFFECTS_AS_LEVEL INT_VAR match_opcode=317 duration=2 multi_duration=2 END

	OUTER_SPRINT c4ef_old1 @190223005
	OUTER_SPRINT c4ef_new1 @190223006
	OUTER_SPRINT c4ef_old2 @190223007
	OUTER_SPRINT c4ef_new2 @190223008
	
	ALTER_TLK BEGIN
		REPLACE_TEXTUALLY ~%c4monk_old%~ ~%c4monk_new%~
		REPLACE_TEXTUALLY ~%c4ef_old1%~ ~%c4ef_new1%~
		REPLACE_TEXTUALLY ~%c4ef_old2%~ ~%c4ef_new2%~
	END
		
	//4级身法
	COPY_EXISTING c4mkbdd.2da override
		SET_2DA_ENTRY 0 1 3 ~c4mkbd1d~
		SET_2DA_ENTRY 1 1 3 ~c4mkbd2d~
		SET_2DA_ENTRY 2 1 3 ~c4mkbd3d~
		BUT_ONLY
		
	//战神
	COPY_EXISTING_REGEXP GLOB ~c4QAS2[ABC].spl~ override
		LPF ALTER_EFFECT INT_VAR check_globals=0 target=2 END
		BUT_ONLY
		
	//绽放
	COPY_EXISTING c4qdc3x.spl override
		SAY 0x50 @190223001
	COPY_EXISTING c4qdc3y.spl override
		SAY 0x50 @190223002
	COPY_EXISTING c4qdgc3.spl override
		SAY 0x50 @190223003
		
	COPY_EXISTING c4qdc3b.spl override
		LPF ALTER_EFFECT INT_VAR match_opcode=17 match_parameter1=6 parameter1=2 END
		LPF ALTER_EFFECT INT_VAR match_opcode=17 match_parameter1=0 dicenumber=6 dicesize=6 END
		
	COPY_EXISTING c4qdc3c.spl override
		LPF ALTER_EFFECT INT_VAR match_opcode=17 match_parameter1=9 parameter1=3 END
		LPF ALTER_EFFECT INT_VAR match_opcode=17 match_parameter1=0 dicenumber=9 dicesize=6 END
	
	//mrk
	COPY_EXISTING ring01.itm ~%marker_loc%/20190223.mrk~

END
