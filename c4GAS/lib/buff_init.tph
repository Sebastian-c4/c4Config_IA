	
//导入文件
COPY ~.../c4_inlined/blank.2da~ ~%workspace%/buff_spells_fixed.2da~
	APPEND_FILE ~%LIBS%/buff_spells.2da~
	PATCH_BASH_FOR ~%LIBS%/mod~ ~.*\.2da~ BEGIN
		PATCH_IF ~%BASH_FOR_RES%~ BEGIN
			APPEND_FILE ~%BASH_FOR_FILESPEC%~
		END
	END

//法术判断
COPY_EXISTING - hidespl.2da override
	READ_2DA_ENTRIES_NOW rows 4
	FOR (i=1;i<rows;++i) BEGIN
		READ_2DA_ENTRY_FORMER rows i 0 spell
		READ_2DA_ENTRY_FORMER rows i 1 hidden
		READ_2DA_ENTRY_FORMER rows i 3 removed
		PATCH_IF (hidden=1 || removed=1)
					&& (~%spell%~ STRING_CONTAINS_REGEXP SPWI59)
					&& (~%spell%~ STRING_CONTAINS_REGEXP SPPR730)
					&& (~%spell%~ STRING_CONTAINS_REGEXP SPPR150)
					&& (~%spell%~ STRING_CONTAINS_REGEXP SPPR729)
		BEGIN
			SET $ignore(~%spell%~)=1
		END
	END

COPY ~%workspace%/buff_spells_fixed.2da~ ~%workspace%/buff_spells_fixed.2da~
	COUNT_2DA_COLS cols
	READ_2DA_ENTRIES_NOW rows cols
	FOR (i=0;i<rows;++i) BEGIN
		SET do=1
		READ_2DA_ENTRY_FORMER rows i 0 spell
		READ_2DA_ENTRY_FORMER rows i 1 default
		READ_2DA_ENTRY_FORMER rows i 2 type
		READ_2DA_ENTRY_FORMER rows i 3 check_op
		READ_2DA_ENTRY_FORMER rows i 4 check_p2
		READ_2DA_ENTRY_FORMER rows i 5 mother
		READ_2DA_ENTRY_FORMER rows i 6 scripts
		PHP_EACH ignore AS hidden => int BEGIN
			PATCH_IF (~%hidden%~ STR_EQ ~%spell%~) BEGIN SET do=0 END
		END
		//存在、未被隐藏，有效且最大持续时间>=30或永久(#146)
		PATCH_IF do && (FILE_EXISTS_IN_GAME ~%spell%.spl~) BEGIN
			INNER_ACTION BEGIN
				COPY_EXISTING - ~%spell%.spl~ override
					SET valid=0
					SET stats=0
					GET_OFFSET_ARRAY header SPL_V10_HEADERS
					PHP_EACH header AS int => index BEGIN
						GET_OFFSET_ARRAY2 effect index SPL_V10_HEAD_EFFECTS
						PHP_EACH effect AS int => offset BEGIN
							READ_SHORT offset opcode
							READ_LONG offset+8 param2
							READ_BYTE offset+0xc timing
							READ_LONG offset+0xe duration
							PATCH_IF valid=0 && (opcode=check_op || check_op=~-1~)
								&& ((timing=1 || timing=9) || (timing=0 && duration>=30))
							BEGIN
								PATCH_IF check_p2=param2 || check_p2=~-1~ BEGIN	SET valid=1	END
							END
							PATCH_IF stats=0 && (opcode=233 || opcode=282 || opcode=328) BEGIN SET stats=1 END
						END
					END
					PATCH_IF valid=0 BEGIN
						SPRINT default ~*~
					END ELSE BEGIN
						PATCH_IF (stats=0) && (~%type%~ STRING_CONTAINS_REGEXP ~_res~) && (~%scripts%~ STR_EQ ~*~) BEGIN
							SPRINT scripts ~SPLSTATE,c4AI_Buff_%spell%~
							PATCH_DEFINE_ASSOCIATIVE_ARRAY need_patch BEGIN
								~%spell%~, ~%check_op%~ => ~%check_p2%~
							END
						END
					END
			END
		END ELSE SPRINT default ~*~
		SET_2DA_ENTRY i 1 cols ~%default%~
		SET_2DA_ENTRY i 6 cols ~%scripts%~
		//母法术已存在则跳过子法术, 延迟类(法免)除外
		PATCH_IF (IS_AN_INT ~%default%~) && (~%type%~ STRING_CONTAINS_REGEXP ~_del~) && (~%mother%~ STR_EQ ~*~) BEGIN
			SET $valid_spells(~%spell%~)=1
		END
		PHP_EACH valid_spells AS xx => int BEGIN
			PATCH_IF ~%xx%~ STR_EQ ~%mother%~ BEGIN
				SET_2DA_ENTRY i 1 cols ~*~
			END
		END
	END

/* 不进行状态判断
ACTION_PHP_EACH need_patch AS spell => param2 BEGIN
	LAF ADD_IDS_ENTRY STR_VAR idsFile=splstate.ids identifier=~c4AI_Buff_%spell%~ END
	OUTER_SET value = IDS_OF_SYMBOL (~splstate~ ~c4AI_Buff_%spell%~)
	COPY_EXISTING ~%spell%.spl~ override
		LPF CLONE_EFFECT INT_VAR multi_match=1
			match_opcode=~%spell_1%~ match_parameter2=param2
			opcode=328 parameter2=value special=1
		END
		BUT_ONLY
END
*/